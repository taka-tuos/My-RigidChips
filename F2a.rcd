// [RCD] 
Val
{
	BRAKE(default=0, max=512, step=256)
	DRS(default=20, min=10, max=20, step=4)
	DRSF(default=0, min=-10, max=0, step=4)
	ENGINE(default=0, min=-8192, max=32768, step=1024)
	HANDLE(default=0, min=-24, max=24, step=4)
	TURBO(default=0, max=65536, step=4096)
}
Key
{
	0:ENGINE(step=1024)
	1:ENGINE(step=-512)
	2:HANDLE(step=-4)
	3:HANDLE(step=4)
	4:BRAKE(step=256)
	5:DRS(step=-4), DRSF(step=-4), TURBO(step=4096)
}
Body
{
	Core() {
		S:Chip() {
			S:Chip() {
				S:Chip() {
					E:Chip(angle=90) {
						E:Cowl(angle=-90) {
							E:Cowl(angle=-90) {
								E:Cowl() { }
							}
						}
					}
					W:Chip(angle=90) {
						W:Cowl(angle=-90) {
							W:Cowl(angle=-90) {
								W:Cowl() { }
							}
						}
					}
					E:Trim(angle=-DRS) { }
					W:Trim(angle=DRS) { }
				}
				W:Frame() {
					W:Wheel(angle=90, power=-ENGINE, brake=BRAKE, option=1, effect=5) { }
				}
				E:Frame() {
					E:Wheel(angle=90, power=ENGINE, brake=BRAKE, option=1, effect=5) { }
				}
				S:Chip(angle=-DRS) {
					W:Chip() { }
					E:Chip() { }
				}
				S:Jet(angle=90, power=-TURBO) { }
			}
		}
		N:Chip() {
			N:Chip() {
				W:Chip() {
					W:Chip(angle=90) {
						S:Cowl() {
							S:Cowl() {
								S:Cowl() { }
							}
						}
						W:Weight(angle=90, option=2) {
							S:Chip(angle=20) { }
						}
					}
				}
				E:Chip() {
					E:Chip(angle=90) {
						S:Cowl() {
							S:Cowl() {
								S:Cowl() { }
							}
						}
						E:Weight(angle=90, option=2) {
							S:Chip(angle=20) { }
						}
					}
					S:Cowl() {
						S:Cowl() {
							S:Cowl() { }
							W:Cowl(angle=-90) {
								S:Cowl() { }
								W:Cowl(angle=90) { }
							}
						}
					}
				}
				N:Chip(angle=0) {
					E:Chip(angle=90) {
						N:Chip() {
							N:Chip() {
								E:Cowl(angle=-90) {
									E:Cowl(angle=-90) { }
								}
							}
						}
					}
					W:Chip(angle=90) {
						N:Chip() {
							N:Chip() {
								W:Cowl(angle=-90) {
									W:Cowl(angle=-90) { }
								}
							}
						}
					}
					W:RudderF(angle=HANDLE) {
						W:Wheel(angle=90, power=-ENGINE, brake=BRAKE, option=1, effect=5) { }
					}
					E:RudderF(angle=HANDLE) {
						E:Wheel(angle=90, power=ENGINE, brake=BRAKE, option=1, effect=5) { }
					}
					N:Chip(angle=20) {
						N:Chip(angle=0) {
							W:Trim(angle=DRSF) { }
							E:Trim(angle=-DRSF) { }
						}
					}
				}
			}
			W:Cowl() {
				S:Cowl() {
					S:Cowl() { }
					E:Cowl(angle=-90) {
						S:Cowl() { }
					}
				}
			}
		}
	}
}
Script
{	print 0,"Welcome to Rigid-Chips World."
	print 1,"  FPS=",_FPS(),"  Chips=",_CHIPS(),"  Weight=",_WEIGHT()
	print 2,"  Width=",_WIDTH(),"  Height=",_HEIGHT()
	print 3,"  Faces=",_FACE()
	print 4,"  Vel=",_VEL()
	print 5,"  R=",_RED(32,32)
	print 6,"  G=",_GREEN(32,32)
	print 7,"  B=",_BLUE(32,32)
}
