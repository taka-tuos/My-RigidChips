// [RCD] BasicCar
Val
{
	BRAKE(default=0, max=80, step=80)
	ENGINE(default=0, min=-32500, max=2500, step=3500)
	HANDLE(default=0, min=-30, max=30, step=5)
	HBRAKE(default=0, max=100, step=100)
}
Key
{
	0:Engine(step=-3500)
	1:Engine(step=3500)
	2:Handle(step=-5)
	3:Handle(step=5)
	7:Brake(step=30), HBrake(step=20)
	8:HBrake(step=20)
}
Body
{
	Core() {
		N:Chip() {
			N:Chip() {
				W:Frame() {
					W:RudderF(angle=Handle, option=1) {
						E:Frame(angle=90, option=1) {
							W:Rudder(angle=-90) { }
						}
						E:RLW(angle=90, power=-Engine, brake=Brake, effect=2) { }
					}
				}
				E:Frame() {
					E:RudderF(angle=Handle, option=1) {
						W:Frame(angle=90, option=1) {
							E:Rudder(angle=90) { }
						}
						W:RLW(angle=90, power=Engine, brake=Brake, effect=2) { }
					}
				}
				N:Frame(option=1) { }
			}
			N:Frame(angle=45) {
				W:Trim(angle=-20) { }
				E:Trim(angle=20) { }
			}
		}
		S:Chip() {
			W:Frame() {
				W:Frame(angle=90, option=1) {
					E:Rudder(angle=90) { }
				}
				W:RLW(angle=90, power=Engine, brake=HBrake, effect=2) { }
			}
			E:Frame() {
				E:Frame(angle=90, option=1) {
					W:Rudder(angle=-90) { }
				}
				E:RLW(angle=90, power=-Engine, brake=HBrake, effect=2) { }
			}
			S:Frame(angle=135) {
				W:Trim(angle=-20) { }
				E:Trim(angle=20) { }
			}
		}
	}
}
Script
{
	print 0,"Welcome to Rigid-Chips World."
	print 1,"  FPS=",_FPS(),"  Chips=",_CHIPS(),"  Weight=",_WEIGHT()
	print 2,"  Width=",_WIDTH(),"  Height=",_HEIGHT()
	print 3,"  Faces=",_FACE()
	print 4,"  Vel=",_VEL()
	print 5,"  R=",_RED(32,32)
	print 6,"  G=",_GREEN(32,32)
	print 7,"  B=",_BLUE(32,32)
}
