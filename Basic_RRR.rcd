// [RCD] BasicCar
Val
{
	BRAKE(default=0, max=80, step=80)
	ENGINE(default=0, min=-15000, max=2500, step=11500)
	HANDLE(default=0, min=-20, max=20, step=5)
	HBRAKE(default=0, max=100, step=100)
}
Key
{
	0:Engine(step=-500)
	1:Engine(step=500)
	2:Handle(step=-5)
	3:Handle(step=5)
	7:Brake(step=30), HBrake(step=20)
	8:HBrake(step=20)
}
Body
{
	Core() {
		S:Frame() {
			W:Frame() {
				W:Wheel(angle=90, power=Engine, brake=HBrake) { }
				N:Chip(angle=-170) { }
			}
			E:Frame() {
				E:Wheel(angle=90, power=-Engine, brake=HBrake) { }
				N:Chip(angle=-170) { }
			}
		}
		N:Chip() {
			N:Frame() {
				W:Frame() {
					W:RudderF(angle=Handle, option=1) {
						E:Wheel(angle=90, power=-Engine, brake=Brake) { }
					}
					N:Chip(angle=-170) { }
				}
				E:Frame() {
					E:RudderF(angle=Handle, option=1) {
						W:Wheel(angle=90, power=Engine, brake=Brake) { }
					}
					N:Chip(angle=-170) { }
				}
			}
		}
		S:Trim(angle=90) {
			N:Chip() { }
		}
	}
}
Script
{
	print 0,"Welcome to Rigid-Chips World."
	print 1,"  FPS=",_FPS(),"  Chips=",_CHIPS(),"  Weight=",_WEIGHT()
	print 2,"  Width=",_WIDTH(),"  Height=",_HEIGHT()
	print 3,"  Faces=",_FACE()
	print 4,"  Vel=",_VEL()
	print 5,"  R=",_RED(32,32)
	print 6,"  G=",_GREEN(32,32)
	print 7,"  B=",_BLUE(32,32)
}
