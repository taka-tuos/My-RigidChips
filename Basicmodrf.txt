// [RCD] BasicCar
Val
{
	BRAKE(default=0, max=80, step=80)
	ENGINE(default=0, min=-42500, max=2500, step=3500)
	HANDLE(default=0, min=-25, max=25, step=5)
	HBRAKE(default=0, max=100, step=100)
}
Key
{
	0:Engine(step=-3500)
	1:Engine(step=3500)
	2:Handle(step=-5)
	3:Handle(step=5)
	7:Brake(step=30), HBrake(step=20)
	8:HBrake(step=20)
}
Body
{
	Core() {
		N:Chip() {
			N:Chip() {
				W:Frame() {
					W:RudderF(angle=Handle, option=1) {
						E:RLW(angle=90, power=-Engine, brake=Brake, effect=2) { }
					}
				}
				E:Frame() {
					E:RudderF(angle=Handle, option=1) {
						W:RLW(angle=90, power=Engine, brake=Brake, effect=2) { }
					}
				}
			}
			N:Frame(angle=45) {
				E:Trim(angle=25) { }
				W:Trim(angle=-25) { }
				N:Chip(angle=135) {
					N:Frame(angle=20) { }
				}
			}
		}
		S:Chip() {
			W:Frame() {
				W:RLW(angle=90, power=Engine, brake=HBrake, effect=2) { }
			}
			E:Frame() {
				E:RLW(angle=90, power=-Engine, brake=HBrake, effect=2) { }
			}
			S:Frame(option=1) {
				N:Frame(angle=45) {
					E:Trim(angle=25) { }
					W:Trim(angle=-25) { }
					N:Chip(angle=-45) {
						N:Frame() {
							W:Chip(angle=-89.99999) {
								S:Chip() { }
							}
							E:Chip(angle=-89.99999) {
								S:Chip() { }
							}
						}
					}
				}
			}
		}
	}
}
Script
{
	print 0,"Welcome to Rigid-Chips World."
	print 1,"  FPS=",_FPS(),"  Chips=",_CHIPS(),"  Weight=",_WEIGHT()
	print 2,"  Width=",_WIDTH(),"  Height=",_HEIGHT()
	print 3,"  Faces=",_FACE()
	print 4,"  Vel=",_VEL()
	print 5,"  R=",_RED(32,32)
	print 6,"  G=",_GREEN(32,32)
	print 7,"  B=",_BLUE(32,32)
}
